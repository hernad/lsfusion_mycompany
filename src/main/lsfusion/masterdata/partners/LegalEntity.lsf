MODULE LegalEntity;

REQUIRE Partner, MasterData;

NAMESPACE MasterData;

CLASS LegalEntity 'Organization';

name 'Name' = DATA ISTRING[50] (LegalEntity) CHARWIDTH 12;
website 'Web site' = DATA ISTRING[30](LegalEntity);

// Наследуем организацию от контрагента
EXTEND CLASS LegalEntity : Partner;

name (LegalEntity l) += name(l);

EXTEND CLASS PartnerType {
    legalEntity 'Organization'
}

type (Partner p) += WHEN p IS LegalEntity THEN PartnerType.legalEntity;

EXTEND FORM partner
    PROPERTIES(p) name[LegalEntity], website
;

DESIGN partner {
    headerDetails {
        MOVE PROPERTY(name(p));
    }
    headerColumn2 {
        MOVE PROPERTY(website(p));
    }
}

newLegalEntity 'Organization' () {
    NEWSESSION {
        NEW l = LegalEntity {
            
            SHOW partner OBJECTS p = l;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM partners
    PROPERTIES newLegalEntity() DRAW p TOOLBAR
    
    LIST LegalEntity OBJECT p
;
EXTEND FORM dialogPartners
    FILTERGROUP type
        FILTER 'Organizations' p IS LegalEntity

    PROPERTIES newLegalEntity() DRAW p TOOLBAR
;
