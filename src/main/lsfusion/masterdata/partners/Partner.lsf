MODULE Partner;

REQUIRE Utils, MetaNumerator, Input;

NAMESPACE MasterData;

CLASS ABSTRACT Partner 'Partner';

@defineID(partner, 'Partners', 'PN', 4);

name 'Name' = ABSTRACT ISTRING[92] (Partner) IN id CHARWIDTH 15 NONULL;

address 'Address' = DATA ISTRING[50] (Partner);
city 'City' = DATA ISTRING[30] (Partner);
state 'Region' = DATA ISTRING[30] (Partner);
zip 'Postcode' = DATA ISTRING[10] (Partner);

csvAddress 'Address' (Partner p) = CONCAT ', ', address(p), city(p), state(p), zip(p);
linesAddress 'Address' (Partner p) = CONCAT '\n ', address(p), city(p), state(p), zip(p);

phone 'Phone' = DATA ISTRING[30] (Partner);
email 'Email' = DATA ISTRING[30] (Partner);

archived 'Inactive' = DATA BOOLEAN (Partner);
active 'Active' (Partner p) = NOT archived(p);

// type
CLASS PartnerType 'Partner\'s type';

name 'Name' (PartnerType o) = staticCaption(o) IF o IS PartnerType CHARWIDTH 15;

type  = ABSTRACT CASE PartnerType (Partner);
nameType 'Partner\'s type' (Partner p) = name(type(p));

FORM partner 'Partner'
    OBJECTS p = Partner PANEL 
    PROPERTIES(p) id, nameType, 
                  address, city, state, zip,
                  phone, email,
                  archived
    
    EDIT Partner OBJECT p
;

DESIGN partner {
    OBJECTS {
        NEW header {
            type = CONTAINERH;            
            NEW headerLeft {
                NEW headerTop {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    MOVE PROPERTY(id(p)); 
                    MOVE PROPERTY(nameType(p));
                }
                NEW headerDetails { alignment = STRETCH; }
                NEW headerPane {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    NEW headerColumn1 {
                        MOVE PROPERTY(address(p));
                        MOVE PROPERTY(city(p));
                        MOVE PROPERTY(state(p));
                        MOVE PROPERTY(zip(p));
                    }
                    NEW headerColumn2 {
                        MOVE PROPERTY(phone(p));
                        MOVE PROPERTY(email(p));
                    }
                    NEW headerColumn3 {
                        MOVE PROPERTY(archived(p));                   
                    }
                }
            }
            NEW headerRight;    
        }
        NEW tabs {
            type = TABBED;
            fill = 1;
            NEW legalInfo {
                caption = 'Legal data';
                NEW legalInfoOptions;
            }
        }
    }
}

FORM partners 'Partners'
    OBJECTS p = Partner
    PROPERTIES(p) READONLY name, id, nameType, csvAddress, phone, email 
    PROPERTIES(p) NEWSESSION EDIT, DELETE 
    FILTERGROUP active
        FILTER 'Active' active(p) 'F10' DEFAULT
        FILTER 'Inactive' archived(p) 'F9'
;

DESIGN partners {
    OBJECTS {
        NEW header {
            fill = 1;
            MOVE BOX(p);
        }
        NEW operations {
            type = TABBED;
            fill = 1;
        }
    }
}

FORM dialogPartners 'Select Partner'
    OBJECTS p = Partner
    PROPERTIES(p) READONLY name, id, nameType
    PROPERTIES(p) NEWSESSION EDIT
    ORDERS name(p)
     
    FILTERGROUP active
        FILTER 'Active' active(p) 'F10' DEFAULT
        FILTER 'Inactive' archived(p) 'F9'

    PROPERTIES() nameFilter, clearNameFilter
    FILTERS isISubstring(name(p), nameFilter()) OR NOT nameFilter()
    
    EVENTS ON INIT { nameFilter() <- NULL; }
    
    LIST Partner OBJECT p
;

NAVIGATOR {
    masterData {
        NEW partners;
    }
}