MODULE UomImport;

REQUIRE Uom;

NAMESPACE MasterData;

uomExport 'Exporting Units' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF Uom i AND i IS Uom) THEN {
            NEW i = Uom {
                id(i) <- '1';
                name(i) <- 'Thing';
            }
        }
    
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Code' = id(Uom i), 'Name' = name(i) TO f;   
        open(f());
    }
}

uomImport 'Importing Units' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL name = ISTRING[50] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id = A, name = B;
            
            FOR imported(INTEGER i) DO NEW im = Uom {
                id(im) <- id(i);
                name(im) <- name(i);
            }
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }    
}

EXTEND FORM migrationData 
    PROPERTIES uomExport(), uomImport()
;

DESIGN migrationData {
    commons {
        NEW uom {
            type = CONTAINERH;
            caption = 'Units';
            MOVE PROPERTY(uomImport());
            MOVE PROPERTY(uomExport());
        }
    }
}