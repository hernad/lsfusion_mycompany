MODULE Item;

REQUIRE Category, Uom, MetaNumerator;

PRIORITY System;

NAMESPACE MasterData;

CLASS ABSTRACT Item 'Items';
class 'View' (Item i) = objectClassName(i) IF i IS Item CHARWIDTH 6;

@defineID(item, 'Items', 'NM', 6);

dataName 'Name' = DATA ISTRING[200] (Item) IN id NONULL CHARWIDTH 20;
prefixName 'Prefix' = ABSTRACT STRING (Item);
suffixName 'Suffix' = ABSTRACT STRING (Item);

name 'Name (full)' (Item i) = CONCAT ' ', prefixName(i), dataName(i), suffixName(i) CHARWIDTH 30 MATERIALIZED INDEXED;

reference 'Article' = DATA STRING[50] (Item) CHARWIDTH 10;

description 'Description' = DATA TEXT (Item);

category 'Category' = DATA Category (Item) NONULL INDEXED;
idCategory 'Category code' (Item i) = id(category(i));
nameCategory 'Category' (Item i) = name(category(i));
canonicalNameCategory 'Category (full)' (Item i) = canonicalName(category(i));

// uom
uom = DATA Uom (Item);
idUom 'Unit change code' (Item i) = id(uom(i));
nameUom 'UOM name' (Item i) = name(uom(i));

CONSTRAINT DROPPED(Uom u IS Uom) AND PREV(uom(Item i)) MESSAGE 'You cannot delete the unit of measure referenced by the item'; 

dataDefaultUom = DATA Uom (Category);
nameDataDefaultUom 'Unit of measurement' (Category c) = name(dataDefaultUom(c));
EXTEND FORM category PROPERTIES nameDataDefaultUom(c);
DESIGN category { default { MOVE PROPERTY(nameDataDefaultUom(c)); } }

defaultUom (Category c) = GROUP LAST dataDefaultUom(Category parent) ORDER DESC level(c, parent);
WHEN LOCAL SETCHANGED(category(Item i)) AND NOT uom(i) DO uom(i) <- defaultUom(category(i));

archived 'Inactive' = DATA BOOLEAN (Item) CHARWIDTH 10;
active 'Active' (Item i) = NOT archived(i) CHARWIDTH 10;

level1 'Group 1' (Item i) = nameCategory1(category(i));
level2 'Group 2' (Item i) = nameCategory2(category(i));
level3 'Group 3' (Item i) = nameCategory3(category(i));
level4 'Group 4' (Item i) = nameCategory4(category(i));

readonly = ABSTRACT BOOLEAN (Item);

copy ABSTRACT LIST (Item, Item);
copy (Item n, Item o) + {
    dataName(n) <- dataName(o);
    category(n) <- category(o);
    uom(n) <- uom(o);
    archived(n) <- archived(o);
}

FORM item 'Items'
    OBJECTS i = Item PANEL
    PROPERTIES(i) dataName READONLYIF readonly(i), class READONLY, 
                  name READONLY, id, canonicalNameCategory READONLYIF readonly(i), nameUom READONLYIF readonly(i),
                  archived READONLYIF readonly(i),
                  reference READONLYIF readonly(i), '' = description READONLYIF readonly(i)
      
    EDIT Item OBJECT i
;

DESIGN item {
    caption = CONCAT ' ', class(i), name(i);
    OBJECTS {        
        NEW statusPane FIRST {
            caption = 'Statuses';
            type = CONTAINERH;
            alignment = STRETCH;                          
            NEW statusActions {
                type = CONTAINERH;                    
                flex = 1; 
                NEW primaryActions {
                    type = CONTAINERH;
                }
                NEW secondaryActions {
                    type = CONTAINERH;
                }           
            }
            NEW status {
                type = CONTAINERH;
            }
        }    
        NEW header {                          
            NEW row1 {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(dataName(i));
                MOVE PROPERTY(class(i));
            }
            MOVE PROPERTY(name(i)) { alignment = STRETCH; }                   
            MOVE PROPERTY(canonicalNameCategory(i));
            NEW pane {
                type = CONTAINERH;
                alignment = STRETCH;
                NEW column1 {
                    MOVE PROPERTY(id(i));
                    MOVE PROPERTY(reference(i));
                }        
                NEW column2 {
                    MOVE PROPERTY(nameUom(i));
                }        
                NEW column3 {
                    MOVE PROPERTY(archived(i));
                }
            }    
        }
        NEW tabs {
            type = TABBED;
            fill = 1;
            NEW operations {
                type = TABBED;
                caption = 'Documents';
            }
            NEW description {
                caption = 'Description';
                MOVE PROPERTY(description(i)) { fill = 1; };
            }
        }
    }  
}

FORM items 'Items'
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDERS name(c)

    OBJECTS i = Item
    PROPERTIES(i) READONLYIF isReadonly() name, id, nameCategory, nameUom, reference, class READONLY
    PROPERTIES(i) SHOWIF isReadonly() NEWSESSION EDIT, DELETE
    ORDERS name(i) 
    FILTERS level(category(i), c)
    FILTERGROUP active
        FILTER 'Active' active(i) 'F8' DEFAULT 
        FILTER 'Inactive' archived(i) 'F7'
        
    LIST Item OBJECT i
;

@extendFormEditable(items);

DESIGN items {
    OBJECTS {
        NEW pane {
            type = SPLITH;
            fill = 1;
            NEW options {
                type = SPLITV;
                NEW categories {
                    fill = 1;
                    MOVE BOX(TREE categories);
                }
                NEW tabs {
                    fill = 1;
                    type = TABBED;
                }
            }                
            NEW details {
                fill = 2; 
                MOVE BOX(i) { 
                    GRID(i) { defaultComponent = TRUE; }
                }
            }

        }
    }
}

NAVIGATOR {
    masterData {
        NEW items;
    }
}
