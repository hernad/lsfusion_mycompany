MODULE CountryImport;

REQUIRE CountryP;

NAMESPACE MasterData;

countryExport 'Export Countries' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Code' = id(Country c), 'Name' = name(c),
                                'Alpha-2' = alpha2(c),  'Alpha-3' = alpha3(c) TO f;   
        open(f());
    }
}

countryImport 'Import countries' () {
    LOCAL id = STRING[3] (INTEGER);
    LOCAL name = ISTRING[50] (INTEGER);
    LOCAL alpha2 = ISTRING[2] (INTEGER);
    LOCAL alpha3 = ISTRING[3] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id = A, name = B, alpha2 = C, alpha3 = D;
            
            FOR imported(INTEGER i) NEW im = Country DO {
                id(im) <- id(i);
                name(im) <- name(i);
                alpha2(im) <- alpha2(i);
                alpha3(im) <- alpha3(i);
            }
            APPLY;
            MESSAGE 'Import completed';
        }
    }    
}

EXTEND FORM migrationData 
    PROPERTIES countryExport(), countryImport()
;

DESIGN migrationData {
    commons {
        NEW country {
            type = CONTAINERH;
            caption = 'Countries';
            MOVE PROPERTY(countryImport());
            MOVE PROPERTY(countryExport());
        }
    }
}