MODULE InvoiceType;

REQUIRE Numerator, InvoicingSettings;

NAMESPACE Invoicing;

CLASS InvoiceType 'Implementation type';
name 'Name' = DATA ISTRING[50] (InvoiceType) NONULL;

id 'Code' = DATA STRING[20] (InvoiceType);
invoiceType = GROUP AGGR InvoiceType t BY id(t);

numerator 'Numerator' = DATA Numerator (InvoiceType);
nameNumerator 'Numerator' (InvoiceType o) = name(numerator(o));

taxIncluded 'Price includes taxes' = DATA BOOLEAN (InvoiceType);

FORM invoiceType 'Implementation type'
    OBJECTS o = InvoiceType PANEL 
    PROPERTIES(o) name, id, nameNumerator, taxIncluded
     
    EDIT InvoiceType OBJECT o;
;

DESIGN invoiceType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
        }
    }
}

FORM invoiceTypes 'Implementation types'
    OBJECTS o = InvoiceType
    PROPERTIES(o) READONLY name, id
    
    LIST InvoiceType OBJECT o;
;

EXTEND FORM options
    OBJECTS it = InvoiceType
    PROPERTIES(it) READONLY name, id
    PROPERTIES(it) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(it);
    }
}

// default type
countInvoiceTypes 'Number of types' () = GROUP SUM 1 IF r IS InvoiceType; 
firstInvoiceType = GROUP LAST InvoiceType r ORDER DESC r WHERE r IS InvoiceType;

defaultInvoiceType = firstInvoiceType() IF countInvoiceTypes() = 1;