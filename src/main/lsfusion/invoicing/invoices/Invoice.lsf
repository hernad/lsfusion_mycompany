MODULE Invoice;

REQUIRE InvoicingSettings, TaxItem, BankAccount, 
        MetaTax, PaymentTermsSales, DateUtils,
        ItemSales, PartnerSales, Company, 
        BarCode, MetaNumerator, Doc,
        InvoiceType, Product;

NAMESPACE Invoicing;

CLASS Invoice 'Implementation';

@defineDocStatus(invoice, 'accounts');

// type
type 'Type' = DATA InvoiceType (Invoice) NONULL;
nameType 'Type' (Invoice s) = name(type(s));

WHEN LOCAL SET(Invoice s IS Invoice) AND NOT CHANGED(type(s)) DO type(s) <- defaultInvoiceType(); 

// dateTime
dateTime 'Date' = DATA DATETIME (Invoice) NONULL IN id;
WHEN LOCAL SET (Invoice b IS Invoice) DO dateTime(b) <- currentDateTime();
@defineDateTimeAggregation(Invoice);

@defineNumberType(invoice);

numberDate 'Description' (Invoice i) = number(i) + ' from ' + dateTime(i) IN id;

// customer
customer = DATA Partner (Invoice);
nameCustomer 'Customer' (Invoice b) = name(customer(b));
CONSTRAINT customer(Invoice b) AND NOT isCustomer(customer(b)) 
                CHECKED BY customer
                MESSAGE 'The Partner must be the Customer';

// company
company 'Company' = DATA Company (Invoice) NONULL;
nameCompany 'Company' (Invoice b) = name(company(b));
WHEN LOCAL SET(Invoice b IS Invoice) AND NOT CHANGED(company(b)) DO company(b) <- defaultCompany(); 

// payment
paymentTerms = DATA PaymentTerm (Invoice); 
namePaymentTerms 'Terms of payment' (Invoice i) = name(paymentTerms(i));

dueDateTime 'Pay before' = DATA DATETIME (Invoice);

WHEN LOCAL CHANGED(customer(Invoice b)) AND NOT CHANGED(paymentTerms(b)) DO paymentTerms(b) <- customerPaymentTerms(customer(b)); 

WHEN LOCAL (SETCHANGED(paymentTerms(Invoice i)) OR SETCHANGED(dateTime(i))) AND paymentTerms(i) AND NOT CHANGED(dueDateTime(i)) DO {
    dueDateTime(i) <- sumDay(dateTime(i), days(paymentTerms(i)));
}

// partner account
customerAccount 'Customer\'s account' = DATA Account (Invoice);
nameCustomerAccount 'Customer\'s account' (Invoice b) = name(customerAccount(b));
WHEN LOCAL CHANGED (customer(Invoice b)) DO customerAccount(b) <- defaultAccount(customer(b));

CONSTRAINT customerAccount(Invoice b) AND NOT customer(b) = holder(customerAccount(b))
    CHECKED BY customerAccount
    MESSAGE 'The account must belong to the selected Partner';

// company account
companyAccount 'Company account' = DATA Account (Invoice);
nameCompanyAccount 'Company account' (Invoice b) = name(companyAccount(b));
WHEN LOCAL CHANGED(company(Invoice b)) DO companyAccount(b) <- defaultAccount(company(b));

CONSTRAINT companyAccount(Invoice b) AND NOT company(b) = holder(companyAccount(b))
    CHECKED BY companyAccount
    MESSAGE 'The account must belong to the selected company';

customerReference 'Customer code' = DATA STRING[30] (Invoice);
note 'Note' = DATA ISTRING[50] (Invoice);

// lines
CLASS InvoiceLine 'Implementation line';

invoice 'Implementation' = DATA Invoice (InvoiceLine) NONULL DELETE;

index 'No' = PARTITION SUM 1 ORDER InvoiceLine l BY invoice(l) IN id MATERIALIZED CHARWIDTH 3;

item 'Items' = DATA Item (InvoiceLine) NONULL;
nameItem 'Items' (InvoiceLine l) = name(item(l)) IN id;
idBarCodeItem 'Barcode' (InvoiceLine l) = idBarCode(item(l));
idItem 'Code' (InvoiceLine l) = id(item(l));

CONSTRAINT item(InvoiceLine l) AND NOT canBeSold(item(l))
            CHECKED BY item 
            MESSAGE 'The item is not for sale';

countProductLines 'Number of lines with Products' (Invoice i) = GROUP SUM 1 IF invoice(InvoiceLine l) = i AND item(l) IS Product;

uom = DATA Uom (InvoiceLine);
nameUom 'UOM name' (InvoiceLine l) = name(uom(l));
WHEN LOCAL CHANGED(item(InvoiceLine l)) DO uom(l) <- uom(item(l)); 

description 'Description' = DATA STRING (InvoiceLine); 

quantity 'Quantity' = DATA NUMERIC[16,3] (InvoiceLine) NONULL CHARWIDTH 7;

weight 'Weight' (InvoiceLine l) = NUMERIC[16,3](quantity(l) * weight(item(l)));

price 'Price' = DATA NUMERIC[10,2] (InvoiceLine) NONULL;

defaultPrice = ABSTRACT CASE NUMERIC[10,2] (InvoiceLine);
WHEN LOCAL SETCHANGED(item(InvoiceLine l)) AND NOT CHANGED(price(l)) DO price(l) <- defaultPrice(l);

defaultPrice (InvoiceLine l) += WHEN salesPrice(item(l)) THEN salesPrice(item(l)); 

@defineDocDiscount(invoice);

@defineTaxCalc(invoice, i, sales, resultPrice);
taxIncluded (InvoiceLine l) += taxIncluded(type(invoice(l)));

// Line properties
nameStatus 'Status' (InvoiceLine l) = nameStatus(invoice(l));
type (InvoiceLine l) = type(invoice(l));
number 'Room' (InvoiceLine l) = number(invoice(l));
numberDate 'Description' (InvoiceLine l) = numberDate(invoice(l));
dateTime 'Date' (InvoiceLine l) = dateTime(invoice(l));
customer (InvoiceLine l) = customer(invoice(l));
nameCustomer 'Customer' (InvoiceLine l) = nameCustomer(invoice(l));
nameCompany 'Company' (InvoiceLine l) = nameCompany(invoice(l));

FORM invoiceLines 'Implementation strings'
    OBJECTS l = InvoiceLine
    PROPERTIES(l) READONLY dateTime, number, index, nameItem, quantity, price
    PROPERTIES(l) NEWSESSION EDIT
    
    LIST InvoiceLine OBJECT l
;

edit (InvoiceLine l) + { edit(invoice(l)); } 

FORM invoice 'Implementation'
    OBJECTS  i = Invoice PANEL 
    PROPERTIES(i) nameType, dateTime, number, nameCustomer, nameCompany,
                  namePaymentTerms, dueDateTime, nameCustomerAccount, nameCompanyAccount,
                  customerReference, note
        
    OBJECTS l = InvoiceLine
    PROPERTIES(l) index, nameItem, description, nameUom, idBarCodeItem, idItem, 
                  quantity, price, taxes ON CHANGE changeTax(l), NEW, DELETE 
    FILTERS invoice(l) = i

    EDIT Invoice OBJECT i
;

DESIGN invoice {
    caption = (CONCAT ' ', 'Implementation', 'No' + number(i), 'from ' + dateTime(i));
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            alignment = STRETCH;
            NEW headerLeft {
                MOVE PROPERTY(nameType(i)) { notNull = TRUE; }
                MOVE PROPERTY(dateTime(i));
                MOVE PROPERTY(number(i));
            }
            NEW headerRight {
                MOVE PROPERTY(nameCustomer(i));
                MOVE PROPERTY(nameCompany(i)) { notNull = TRUE; }           
            }
            NEW relatedDoc {
                fill = 1;
                type = TABBED;
            }
        }
        NEW details {
            fill = 5;
            type = TABBED;
            NEW lines {
                caption = 'Strings';
                MOVE BOX(l) { 
                    fill = 3;
                    PROPERTY(nameItem(l)) { notNull = TRUE; } 
                }
                NEW linesFooter {                     
                    type = CONTAINERH;
                    alignment = STRETCH;
                }
            }
            NEW otherInformation {
                caption = 'other information';
                type = COLUMNS;
                columns = 2;
                NEW payment {
                    caption = 'Payment';
                    alignment = STRETCH;
                    MOVE PROPERTY(namePaymentTerms(i)); 
                    MOVE PROPERTY(dueDateTime(i)); 
                    MOVE PROPERTY(nameCompanyAccount(i));
                    MOVE PROPERTY(nameCustomerAccount(i));              
                }           
                NEW invoiceInformat {
                    caption = 'Others';
                    alignment = STRETCH;
                    MOVE PROPERTY(customerReference(i)); 
                    MOVE PROPERTY(note(i));
                }
            }
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

@defineDocHistory(invoice, i, item, quantity);
@defineDocSearch(invoice, item, quantity, 'Quantity', i, canBeSold);
@defineBarCodeSearch(invoice, i, l, item, quantity);

@defineDocDiscountForm(invoice);

FORM invoices 'Implementation'
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY nameType, dateTime, number, nameCustomer, nameCompany,
                           namePaymentTerms, dueDateTime, nameCustomerAccount, nameCompanyAccount,
                           customerReference, note         
    PROPERTIES(i) NEWSESSION NEW, EDIT, DELETE
           
    LIST Invoice OBJECT i
;

@defineTaxForm(invoice, i);
@defineDocStatusForm(invoice, i);
@defineDocLinesCount(invoice, i);
@defineDateFilterForm(invoices, i);

NAVIGATOR {
    operations {
        NEW invoices;
    }
}

// copy
copy 'Copy' (Invoice i)  { 
    NEWSESSION {
        NEW ni = Invoice {
            customer(ni) <- customer(i);
            note(ni) <- note(i);
            FOR invoice(InvoiceLine l) = i INLINE NEW nl = InvoiceLine DO {
                invoice(nl) <- ni;
                item(nl) <- item(l);
                
                quantity(nl) <- quantity(l);
                
                price(nl) <- price(l);
                discount(nl) <- discount(l);
                discountPrice(nl) <- discountPrice(l);
            }
            SHOW invoice OBJECTS i = ni DOCKED;
        }
    }
}
EXTEND FORM invoice PROPERTIES(i) copy;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(copy(i)); 
    }
}
