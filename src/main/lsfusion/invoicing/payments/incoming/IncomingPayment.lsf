MODULE IncomingPayment;

REQUIRE Time, MetaNumerator, Doc, DateUtils,
        Partner, Company, 
        Account, BankAccount, CashAccount,
        IncomingPaymentType;

NAMESPACE Invoicing;

CLASS IncomingPayment 'Входящий платеж';

@defineDocStatus(incomingPayment, 'incoming payment');

// type
type 'Type' = DATA IncomingPaymentType (IncomingPayment) NONULL;
nameType 'Type' (IncomingPayment s) = name(type(s));

dateTime 'Date' = DATA DATETIME (IncomingPayment) NONULL;
dateTime(IncomingPayment p) <- currentDateTime() WHEN SET(p IS IncomingPayment);
@defineDateTimeAggregation(IncomingPayment);

@defineNumberType(incomingPayment);

numberDate 'Description' (IncomingPayment p) = number(p) + ' from ' + dateTime(p) IN id;

// partners
partner 'Partner' = DATA Partner (IncomingPayment);
namePartner 'Partner' (IncomingPayment p) = name(partner(p));

WHEN LOCAL SETCHANGED(type(IncomingPayment p)) AND NOT CHANGED(partner(p)) AND interpay(type(p)) AND countCompany() = 1 DO
    partner(p) <- defaultCompany();

CONSTRAINT interpay(type(IncomingPayment p)) AND NOT partner(p) IS Company
                CHECKED BY partner
                MESSAGE 'With an internal payment';

company 'Company' = DATA Company (IncomingPayment) NONULL;
nameCompany 'Company' (IncomingPayment p) = name(company(p));

WHEN LOCAL SET(IncomingPayment p IS IncomingPayment) AND NOT CHANGED(company(p)) DO
    company(p) <- defaultCompany();

// accounts
// partner
partnerAccount = DATA Account (IncomingPayment);
namePartnerAccount 'Partner account' (IncomingPayment p) = name(partnerAccount(p));

WHEN LOCAL CHANGED(partner(IncomingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO
    partnerAccount(p) <- defaultAccount(partner(p), type(p));
WHEN LOCAL SET(type(IncomingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO
    partnerAccount(p) <- defaultAccount(partner(p), type(p)); 

CONSTRAINT partnerAccount(IncomingPayment p) AND NOT holder(partnerAccount(p)) = partner(p)
                CHECKED BY partnerAccount
                MESSAGE 'The Partner account owner does not match the Partner in the incoming payment';
CONSTRAINT partnerAccount(IncomingPayment p) IS BankAccount AND NOT bank(type(p))
                CHECKED BY partnerAccount
                MESSAGE 'Partner bank accounts are not allowed for this type of payment';
CONSTRAINT partnerAccount(IncomingPayment p) IS CashAccount AND NOT cash(type(p))
                CHECKED BY partnerAccount
                MESSAGE 'Partner cash desks are not allowed for this type of payment.';
                
// company
companyAccount = DATA Account (IncomingPayment);
nameCompanyAccount 'Company account' (IncomingPayment p) = name(companyAccount(p));

WHEN LOCAL CHANGED(company(IncomingPayment p)) AND NOT CHANGED(companyAccount(p)) DO
    companyAccount(p) <- defaultAccount(company(p), type(p));
WHEN LOCAL SET(type(IncomingPayment p)) AND NOT CHANGED(companyAccount(p)) DO
    companyAccount(p) <- defaultAccount(company(p), type(p)); 

CONSTRAINT companyAccount(IncomingPayment p) AND NOT holder(companyAccount(p)) = company(p)
                CHECKED BY companyAccount
                MESSAGE 'The company account holder does not match the company in the incoming payment';
CONSTRAINT companyAccount(IncomingPayment p) IS BankAccount AND NOT bank(type(p))
                CHECKED BY companyAccount
                MESSAGE 'Company bank accounts are not allowed for this type of payment';
CONSTRAINT companyAccount(IncomingPayment p) IS CashAccount AND NOT cash(type(p))
                CHECKED BY companyAccount
                MESSAGE 'For this type of payment';

amount 'Amount' = DATA NUMERIC[14,2] (IncomingPayment) NONULL;

note 'Note' = DATA ISTRING[50] (IncomingPayment);
reference 'Link' = DATA ISTRING[50] (IncomingPayment);

// forms
FORM incomingPayment 'Входящий платеж'
    OBJECTS p = IncomingPayment PANEL 
    PROPERTIES(p) nameType, dateTime, number, amount,
                  namePartner, namePartnerAccount, 
                  nameCompany, nameCompanyAccount,
                  note, reference
               
    
    EDIT IncomingPayment OBJECT p
;

DESIGN incomingPayment {
    caption = (CONCAT ' ', 'Входящий платеж', 'No' + number(p), 'from ' + dateTime(p));
    OBJECTS {
        NEW header {
            NEW params {
                caption = 'Parameters';
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(nameType(p)) { notNull = TRUE; } 
                MOVE PROPERTY(dateTime(p)); 
                MOVE PROPERTY(number(p));
                MOVE PROPERTY(amount(p));
            }
            NEW partner {
                type = CONTAINERH;
                alignment = STRETCH;
                caption = 'Partner';
                MOVE PROPERTY(namePartner(p));
                MOVE PROPERTY(namePartnerAccount(p));
            }
            NEW company { 
                type = CONTAINERH;
                alignment = STRETCH;
                caption = 'Company';
                MOVE PROPERTY(nameCompany(p)) { notNull = TRUE; }
                MOVE PROPERTY(nameCompanyAccount(p));
            }
            NEW otherInformation {
                caption = 'other information';
                alignment = STRETCH;
                MOVE PROPERTY(note(p)); 
                MOVE PROPERTY(reference(p));    
            }
        }
        NEW details {
            type = TABBED;
            fill = 1;
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

@defineDocHistoryHead(incomingPayment, p);

FORM incomingPayments 'Incoming payments'
    OBJECTS p = IncomingPayment
    PROPERTIES(p) READONLY nameType, dateTime, number, amount,
                           namePartner, namePartnerAccount, 
                           nameCompany, nameCompanyAccount,
                           note, reference
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST IncomingPayment OBJECT p
;

@defineDocStatusForm(incomingPayment, p);
@defineDateFilterForm(incomingPayments, p);

NAVIGATOR {
    invoicing {
        operations {
            NEW incomingPayments;
        }
    }
}
