MODULE BillType;

REQUIRE Numerator, InvoicingSettings;

NAMESPACE Invoicing;

CLASS BillType 'Purchase type';
name 'Name' = DATA ISTRING[50] (BillType) NONULL;

id 'Code' = DATA STRING[20] (BillType);
billType = GROUP AGGR BillType t BY id(t);

numerator 'Numerator' = DATA Numerator (BillType);
nameNumerator 'Numerator' (BillType o) = name(numerator(o));

taxIncluded 'Price includes taxes' = DATA BOOLEAN (BillType);

FORM billType 'Purchase type'
    OBJECTS o = BillType PANEL 
    PROPERTIES(o) name, id, nameNumerator, taxIncluded
     
    EDIT BillType OBJECT o;
;

DESIGN billType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
        }
    }
}

FORM billTypes 'Acquisition types'
    OBJECTS o = BillType
    PROPERTIES(o) READONLY name, id
    
    LIST BillType OBJECT o;
;

EXTEND FORM options
    OBJECTS bt = BillType
    PROPERTIES(bt) READONLY name, id
    PROPERTIES(bt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(bt);
    }
}

// default type
countBillType 'Number of types' () = GROUP SUM 1 IF r IS BillType; 
firstBillType = GROUP LAST BillType r ORDER DESC r WHERE r IS BillType;

defaultBillType = firstBillType() IF countBillType() = 1;