MODULE PurchaseOrderType;

REQUIRE Numerator, PurchaseSettings;

NAMESPACE Purchase;

CLASS OrderType 'Order type';
name 'Name' = DATA ISTRING[50] (OrderType) NONULL;

id 'Code' = DATA STRING[20] (OrderType);
orderType = GROUP AGGR OrderType t BY id(t);

numerator 'Numerator' = DATA Numerator (OrderType);
nameNumerator 'Numerator' (OrderType o) = name(numerator(o));

taxIncluded 'Price includes taxes' = DATA BOOLEAN (OrderType);

FORM orderType 'Order type'
    OBJECTS o = OrderType PANEL 
    PROPERTIES(o) name, id, nameNumerator, taxIncluded
     
    EDIT OrderType OBJECT o;
;

DESIGN orderType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
        }
    }
}

FORM orderTypes 'Types of orders'
    OBJECTS o = OrderType
    PROPERTIES(o) READONLY name, id
    
    LIST OrderType OBJECT o;
;

EXTEND FORM options
    OBJECTS ot = OrderType
    PROPERTIES(ot) READONLY name, id
    PROPERTIES(ot) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(ot);
    }
}

// default type
countOrderTypes 'Number of types' () = GROUP SUM 1 IF r IS OrderType; 
firstOrderType = GROUP LAST OrderType r ORDER DESC r WHERE r IS OrderType;

defaultOrderType = firstOrderType() IF countOrderTypes() = 1;