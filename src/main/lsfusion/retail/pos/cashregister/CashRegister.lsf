MODULE CashRegister;

REQUIRE Pos, Session, IncomingPayment, OutgoingPayment, Invoicing,
        PosDashboard, PosDashboardPayment, PosDashboardCash,
        CashRegisterMeta;

NAMESPACE Retail;

CLASS CashDriver 'Fiscal driver';

name 'Name' (CashDriver d) = staticCaption(d) IF d IS CashDriver;

driver 'Driver' = DATA CashDriver (Pos);
nameDriver 'Driver' (Pos p) = name(driver(p));

dataHostname 'Host' = DATA ISTRING[30] (Pos);
hostname 'Host' (Pos p) = OVERRIDE dataHostname(p), 'localhost'; 

FORM cashDrivers 'Drivers'
    OBJECTS d = CashDriver
    PROPERTIES(d) READONLY name
    
    LIST CashDriver OBJECT d
;

EXTEND FORM pos PROPERTIES(p) nameDriver, dataHostname SHOWIF driver(p);    
EXTEND FORM poss PROPERTIES(p) READONLY nameDriver;

session = DATA Session (Payment);

openShift 'Open shift' ABSTRACT CASE EXCLUSIVE (Session);
closeShift 'Close shift' ABSTRACT CASE EXCLUSIVE (Session);
cashIn 'Introduction' ABSTRACT CASE EXCLUSIVE (Payment);
cashOut 'Notch' ABSTRACT CASE EXCLUSIVE (Payment);
buy 'Expense receipt' ABSTRACT CASE EXCLUSIVE (Invoice);

continuePrint 'Print' ABSTRACT CASE EXCLUSIVE (Pos);
reportX 'X-report' ABSTRACT CASE EXCLUSIVE (Pos);

afterOpen(Session s) + {openShift(s);}
afterClose(Session s) + {closeShift(s);}
afterPosPayment(Invoice i) + {buy(i);}
afterDeposit(Pos p, IncomingPayment i) + {
    session(i) <- openedSession(p);
    cashIn(i);
}
afterWithdraw(Pos p, OutgoingPayment o) + {
    session(o) <- openedSession(p);
    cashOut(o);
}

fiscalOpenSession 'Fiscal' = DATA BOOLEAN (Session);
fiscalCloseSession 'Fiscal' = DATA BOOLEAN (Session);

refiscalSession 'Fiscalization' (Session s) {
    IF fiscalOpenSession(s) AND NOT fiscalCloseSession(s) AND closingDateTime(s) THEN
        closeShift(s);
    ELSE
        IF NOT fiscalOpenSession(s) AND NOT closingDateTime(s) THEN
            openShift(s);
}

EXTEND FORM session
    PROPERTIES(s) fiscalOpenSession SHOWIF NOT closingDateTime(s),
                  fiscalCloseSession SHOWIF closingDateTime(s),
                  refiscalSession SHOWIF NOT fiscalCloseSession(s) AND (closingDateTime(s) OR NOT fiscalOpenSession(s)) 
;

DESIGN session {
    header {
        NEW fiscal {
            type = CONTAINERH;
            MOVE PROPERTY(fiscalOpenSession(s));
            MOVE PROPERTY(fiscalCloseSession(s));
            MOVE PROPERTY(refiscalSession(s));
        }
    }
}

@defFiscal(incomingPayment, p, cashIn);
@defFiscal(outgoingPayment, p, cashOut);
@defFiscal(invoice, i, buy);

EXTEND FORM posDashboard PROPERTIES(p) continuePrint, reportX; 

DESIGN posDashboard {
    cashRegister {
        MOVE PROPERTY(continuePrint(p));
        MOVE PROPERTY(reportX(p));
    }
}