MODULE InvLedger;

REQUIRE InventorySettings, Category, Product, Location, Partner, Time;

NAMESPACE Inventory;

CLASS ABSTRACT InvLedger 'Remaining change register';
class 'View' (InvLedger l) = objectClassName(l) IF l IS InvLedger CHARWIDTH 15;

type 'Type' = ABSTRACT ISTRING[100] (InvLedger) CHARWIDTH 20 MATERIALIZED;

active 'Active' = ABSTRACT BOOLEAN (InvLedger) MATERIALIZED;
dateTime 'Time' = ABSTRACT DATETIME (InvLedger) MATERIALIZED;
number 'Room' = ABSTRACT STRING[31] (InvLedger) MATERIALIZED;

fromLocation 'Storage location (from where)' = ABSTRACT Location (InvLedger) MATERIALIZED;
nameFromLocation 'Storage location (from where)' (InvLedger l) = name(fromLocation(l));

toLocation 'Storage location (where)' = ABSTRACT Location (InvLedger) MATERIALIZED;
nameToLocation 'Storage location (where)' (InvLedger l) = name(toLocation(l));

product 'Product' = ABSTRACT Product (InvLedger) MATERIALIZED;
nameProduct 'Product' (InvLedger l) = name(product(l));

INDEX product(InvLedger l);
INDEX fromLocation(InvLedger l), product(l), dateTime(l), l;
INDEX toLocation(InvLedger l), product(l), dateTime(l), l;

quantity 'Quantity' = ABSTRACT NUMERIC[16,3] (InvLedger) MATERIALIZED;

partner = ABSTRACT Partner (InvLedger) MATERIALIZED;
namePartner 'Partner' (InvLedger l) = name(partner(l));

// result  
inQuantity 'Number of received' (Location ll, Product p) = GROUP SUM quantity(InvLedger l) 
                                                    IF active(l) AND toLocation(l) = ll AND product(l) = p MATERIALIZED;
                                                    
inQuantityA (Location ll, Product p, DATETIME dt) = inQuantity(ll, p) (-)
    GROUP SUM quantity(InvLedger l)  IF active(l) AND toLocation(l) = ll AND product(l) = p AND dateTime(l) > dt;

inQuantityB (Location ll, Product p, DATETIME dt) = inQuantity(ll, p) (-)
    GROUP SUM quantity(InvLedger l)  IF active(l) AND toLocation(l) = ll AND product(l) = p AND dateTime(l) >= dt;
                                   
outQuantity 'Quantity sent' (Location ll, Product p) = GROUP SUM quantity(InvLedger l) 
                                                    IF active(l) AND fromLocation(l) = ll AND product(l) = p MATERIALIZED;

outQuantityA 'Quantity sent' (Location ll, Product p, DATETIME dt) = outQuantity(ll, p) (-)
    GROUP SUM quantity(InvLedger l) IF active(l) AND  fromLocation(l) = ll AND product(l) = p AND dateTime(l) > dt;

outQuantityB 'Quantity sent' (Location ll, Product p, DATETIME dt) = outQuantity(ll, p) (-)
    GROUP SUM quantity(InvLedger l) IF active(l) AND  fromLocation(l) = ll AND product(l) = p AND dateTime(l) >= dt;

onHand 'Current balance' (Location ll, Product p) = inQuantity(ll, p) (-) outQuantity(ll, p) MATERIALIZED;
prevOnHand (Location ll, Product p) = PREV(onHand(ll, p));

onHandA 'The remainder' (Location ll, Product p, DATETIME dt) = 
                                                            inQuantityA(ll, p, dt) (-) outQuantityA(ll, p, dt) IF dt;
prevOnHandA (Location ll, Product p, DATETIME dt) = PREV(onHandA(ll, p, dt));

onHandB 'The remainder' (Location ll, Product p, DATETIME dt) = 
                                                            inQuantityB(ll, p, dt) (-) outQuantityB(ll, p, dt) IF dt;
prevOnHandB 'The remainder' (Location ll, Product p, DATETIME dt) = PREV(onHandB(ll, p, dt));

onHandRec 'Current balance (including nesting)' (Location l, Product p) = 
    GROUP SUM onHand(Location child, p) IF level(child, l) MATERIALIZED;

prevOnHandRec 'Current balance (including nesting)' (Location l, Product p) = PREV(onHandRec(l, p)); 
    

// dates
dateReceived 'Date of last arrival' (Location l, Product p) = 
    GROUP LAST dateTime(InvLedger il) ORDER dateTime(il), il WHERE toLocation(il) = l AND product(il) = p MATERIALIZED;
prevDateReceived (Location l, Product p) = PREV(dateReceived(l, p));
    
dateShipped 'Last Ship Date' (Location l, Product p) = 
    GROUP LAST dateTime(InvLedger il) ORDER dateTime(il), il WHERE fromLocation(il) = l AND product(il) = p;
    
datePrevOperation(Location l, Product p, DATETIME dt) = 
    GROUP LAST dateTime(InvLedger il) ORDER dateTime(il), il 
    WHERE (fromLocation(il) = l OR toLocation(il) = l) AND product(il) = p AND dateTime(il) < dt AND active(il);

onHandAFromLocation 'Остаток после операции (откуда)' (InvLedger inv) = onHandA(fromLocation(inv), product(inv), dateTime(inv));
onHandAToLocation 'Остаток после операции (куда)' (InvLedger inv)= onHandA(toLocation(inv), product(inv), dateTime(inv));                                               

// constraint
positiveOnHand 'Prohibiting negative balances' = DATA BOOLEAN (Location) CHARWIDTH 15;
            
recPositiveOnHand 'Prohibiting negative balances' (Location child) = 
    GROUP LAST positiveOnHand(Location parent) ORDER DESC level(child, parent) 
    WHERE positiveOnHand(parent) MATERIALIZED CHARWIDTH 15;
                                                      
CONSTRAINT SET(onHand(Location ll, Product p) < 0) AND recPositiveOnHand(ll) 
            MESSAGE 'The current balance cannot be negative';
            
EXTEND FORM location
    PROPERTIES(l) positiveOnHand
;
DESIGN location {
    params {
        MOVE PROPERTY(positiveOnHand(l));
    }
}

EXTEND FORM locations
    PROPERTIES(l) READONLY recPositiveOnHand
;