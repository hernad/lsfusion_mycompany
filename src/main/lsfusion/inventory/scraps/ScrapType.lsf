MODULE ScrapType;

REQUIRE Numerator, Location;

NAMESPACE Inventory;

CLASS ScrapType 'Тип списания';
name 'Name' = DATA ISTRING[50] (ScrapType) NONULL;

id 'Code' = DATA STRING[20] (ScrapType);
scrapType = GROUP AGGR ScrapType t BY id(t);

numerator = DATA Numerator (ScrapType);
nameNumerator 'Numerator' (ScrapType o) = name(numerator(o));

defaultLocation = DATA Location (ScrapType);
nameDefaultLocation 'Место хранения по умолчанию' (ScrapType o) = name(defaultLocation(o));

FORM scrapType 'Receipt type'
    OBJECTS o = ScrapType PANEL 
    PROPERTIES(o) name, id, nameNumerator, nameDefaultLocation
     
    EDIT ScrapType OBJECT o;
;

DESIGN scrapType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
        }
    }
}

FORM scrapTypes 'Типы поступлений'
    OBJECTS o = ScrapType
    PROPERTIES(o) READONLY name, id
    LIST ScrapType OBJECT o;
;

EXTEND FORM options
    OBJECTS sct = ScrapType
    PROPERTIES(sct) READONLY name, id
    PROPERTIES(sct) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(sct);
    }
}

// default type
countScrapTypes 'Number of types' () = GROUP SUM 1 IF r IS ScrapType; 
firstScrapType = GROUP LAST ScrapType r ORDER DESC r WHERE r IS ScrapType;

defaultScrapType = firstScrapType() IF countScrapTypes() = 1;