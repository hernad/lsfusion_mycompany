MODULE Scrap;

REQUIRE InventorySettings, Product, DateUtils, 
        LocationEmployee, InvLedger, 
        BarCode, Doc, ScrapType;

NAMESPACE Inventory;

CLASS Scrap 'Magazine';

@defineDocStatus(scrap, 'magazines');

// type
type 'Type' = DATA ScrapType (Scrap) NONULL;
nameType 'Type' (Scrap r) = name(type(r));

WHEN LOCAL SET(Scrap r IS Scrap) AND NOT CHANGED(type(r)) DO type(r) <- defaultScrapType(); 

dateTime 'Date' = DATA DATETIME (Scrap) NONULL;
dateTime(Scrap s) <- currentDateTime() WHEN SET(s IS Scrap);
@defineDateTimeAggregation(Scrap);

@defineNumberType(scrap);

numberDate 'Description' (Scrap s) = number(s) + ' from ' + dateTime(s) IN id;

location 'Storage' = DATA Location (Scrap) NONULL;
nameLocation 'Storage' (Scrap s) = name(location(s));

note 'Note' = DATA ISTRING (Scrap) CHARWIDTH 30;

// lines
CLASS ScrapLine 'Debit line';

scrap 'Magazine' = DATA Scrap (ScrapLine) NONULL DELETE;

index 'No' = PARTITION SUM 1 ORDER ScrapLine l BY scrap(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Product' = DATA Product (ScrapLine);
nameProduct 'Product' (ScrapLine l) = name(product(l));
idBarCodeProduct 'Barcode' (ScrapLine l) = idBarCode(product(l));
idProduct 'Code' (ScrapLine l) = id(product(l));

uom (ScrapLine l) = uom(product(l));
nameUom 'UOM name' (ScrapLine l) = name(uom(l));

quantity 'Quantity' = DATA NUMERIC[16,3] (ScrapLine);

FORM scrap 'Magazine'
    OBJECTS s = Scrap PANEL 
    PROPERTIES(s) nameType, dateTime, number, nameLocation, note
    
    OBJECTS l = ScrapLine
    PROPERTIES(l) index, nameProduct, nameUom, idBarCodeProduct, idProduct, quantity, NEW, DELETE
    FILTERS scrap(l) = s
    
    EDIT Scrap OBJECT s
;

DESIGN scrap {
    caption = (CONCAT ' ', 'Magazine', 'No' + number(s), 'from ' + dateTime(s));
    OBJECTS {
        PROPERTY(nameType(s)) { notNull = TRUE; }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW lines {
                caption = 'Strings';
                type = SPLITH;
                MOVE BOX(l);
                NEW linesTab {
                    fill = 0.3;
                    type = TABBED;
                }    
            }
        }
    }
}

@defineDocHistory(scrap, s, product, quantity);

FORM scraps 'Magazines'
    OBJECTS s = Scrap 
    PROPERTIES(s) READONLY nameType, dateTime, number, nameLocation, note
    PROPERTIES(s) NEWSESSION NEW, EDIT, DELETE
    
    LIST Scrap OBJECT s
;

@defineDocStatusForm(scrap, s);
@defineDocLinesCount(scrap, s);
@defineDocLocationAccess(scrap, s);
@defineDateFilterForm(scraps, s);

NAVIGATOR {
    operations {
        NEW scraps;
    }
}


// copy
copy 'Copy' (Scrap s) {
    NEWSESSION {
        NEW ns = Scrap {
            dateTime(ns) <- dateTime(s);
            location(ns) <- location(s);
            FOR scrap(ScrapLine l) = s INLINE NEW nl = ScrapLine DO {
                scrap(nl) <- ns;
                product(nl) <- product(l);
                quantity(nl) <- quantity(l);
            }
            SHOW scrap OBJECTS s = ns DOCKED;
        }
    }
}

EXTEND FORM scrap
    PROPERTIES(s) copy
;

DESIGN scrap {
    secondaryActions {
        MOVE PROPERTY(copy(s));
    }
}