MODULE ReceiptDone;

REQUIRE ReceiptReady;

NAMESPACE Inventory;

EXTEND CLASS ReceiptStatus {
    done 'Accepted'
}
done 'Accepted' = DATA BOOLEAN (Receipt);

status (Receipt r) += WHEN done(r) THEN ReceiptStatus.done;

executionDateTime 'Receipt Date' = DATA DATETIME (Receipt);
WHEN SET(done(Receipt r)) AND NOT CHANGED(executionDateTime(r)) DO executionDateTime(r) <- currentDateTime(); 
executionDateTime 'Receipt Date' (ReceiptLine l) = executionDateTime(receipt(l));

done 'Accepted quantity' = DATA NUMERIC[16,3] (ReceiptLine);

CONSTRAINT done(ReceiptLine l) < 0 OR done(l) > initialDemand(l) 
    MESSAGE 'The accepted amount must be between 0 and the original';

setDone 'Fill Accepted' (Receipt r) {
    done(ReceiptLine l) <- initialDemand(l) WHERE receipt(l) = r;    
}

validate 'Accepted' (Receipt r) {        
    APPLY; // сохраняем изменения
    IF canceled() THEN RETURN; // если сработало ограничение, то выходим
        
    NEWSESSION {
        IF NOT immediate(r) THEN {
            IF NOT GROUP SUM 1 IF done(ReceiptLine l) AND receipt(l) = r THEN
                ASK 'Accepted Receipt Quantities Not Filled ' + number(r) + '. Put as expected?' DO {
                    done(ReceiptLine l) <- initialDemand(l) WHERE receipt(l) = r;
                } ELSE RETURN;
        }
        
        done(r) <- TRUE;
        APPLY;
    }
}

toValidate (Receipt r) = status(r) = ReceiptStatus.ready OR (immediate(r) AND status(r) = ReceiptStatus.draft);

EXTEND FORM receipt
    PROPERTIES(r) validate SHOWIF toValidate(r), 
                  done,
                  setDone DRAW l TOOLBAR SHOWIF status(r) = ReceiptStatus.ready,
                  executionDateTime SHOWIF done(r)
    
    PROPERTIES(l) done BACKGROUND RGB(198,230,247) IF toValidate(r)
;

DESIGN receipt {
    primaryActions {
        MOVE PROPERTY(validate(r)) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(done(r));                              
    } 
    headerLeft {
        MOVE PROPERTY(executionDateTime(r));
    }                  
}

EXTEND FORM receipts 
    PROPERTIES(r) READONLY executionDateTime AFTER scheduledDateTime(r)

    EXTEND FILTERGROUP status
        FILTER 'Accepted' status(r) = ReceiptStatus.done
;