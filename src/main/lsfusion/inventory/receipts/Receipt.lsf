MODULE Receipt;

REQUIRE System, Time, DateUtils,
        InventorySettings, Location, Partner, Product, LocationEmployee,
        ReceiptType, ItemPurchase, PartnerPurchase, BarCode, Doc, ResLedger;

NAMESPACE Inventory;

CLASS Receipt 'Admission';

@defineDocStatus(receipt, 'receipts');

immediate 'Unplanned' = DATA BOOLEAN (Receipt);

// type
type 'Type' = DATA ReceiptType (Receipt) NONULL;
nameType 'Type' (Receipt r) = name(type(r));

WHEN LOCAL SET(Receipt r IS Receipt) AND NOT CHANGED(type(r)) DO type(r) <- defaultReceiptType(); 

scheduledDateTime 'Planned date' = DATA DATETIME (Receipt) NONULL IN id;
scheduledDateTime(Receipt r) <- currentDateTime() WHEN (r IS Receipt); 
@defineDateTimeAggregation(Receipt, scheduled, ' the planned');

@defineNumberType(receipt);
receipt (STRING[31] number) = GROUP LAST Receipt r ORDER r BY number(r);

numberDate 'Description' (Receipt r) = number(r) + ' from ' + scheduledDateTime(r) IN id;

// location
location 'Storage' = DATA Location (Receipt) NONULL INDEXED;
nameLocation 'Storage' (Receipt r) = name(location(r));
WHEN LOCAL SETCHANGED(type(Receipt r)) AND defaultLocation(type(r)) AND NOT CHANGED(location(r)) DO location(r) <- defaultLocation(type(r));

// partner
vendor = DATA Partner (Receipt) INDEXED;
nameVendor 'Vendor' (Receipt r) = name(vendor(r));

CONSTRAINT vendor(Receipt r) AND NOT isVendor(vendor(r)) 
                CHECKED BY vendor[Receipt]
                MESSAGE 'The Partner must be a Vendor';

note 'Note' = DATA ISTRING[50] (Receipt);

// lines
CLASS ReceiptLine 'Receipt line';

receipt 'Document' = DATA Receipt (ReceiptLine) NONULL DELETE INDEXED;

countLines 'Number of lines' (Receipt r) = GROUP SUM 1 IF receipt(ReceiptLine l) = r MATERIALIZED;
index 'No' = PARTITION SUM 1 ORDER ReceiptLine l BY receipt(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Product' = DATA Product (ReceiptLine) NONULL INDEXED;
nameProduct 'Product' (ReceiptLine l) = name(product(l)) IN id;
idBarCodeProduct 'Barcode' (ReceiptLine l) = idBarCode(product(l));
idProduct 'Code' (ReceiptLine l) = id(product(l));

CONSTRAINT product(ReceiptLine l) AND NOT canBePurchased(product(l))
            CHECKED BY product[ReceiptLine]
            MESSAGE 'The product is not intended for purchase';

uom (ReceiptLine l) = uom(product(l));
nameUom 'UOM name' (ReceiptLine l) = name(uom(l));

initialDemand 'Planned quantity' = DATA NUMERIC[16,3] (ReceiptLine);

CONSTRAINT initialDemand(ReceiptLine l) < 0 OR initialDemand(l) > maxQuantity(type(receipt(l))) AND maxQuantity(type(receipt(l))) 
    MESSAGE 'The planned amount should be between 0 and the maximum allowed';
    
// Line properties
nameStatus 'Status' (ReceiptLine l) = nameStatus(receipt(l));
number 'Room' (ReceiptLine l) = number(receipt(l)) IN id;
numberDate 'Description' (ReceiptLine l) = numberDate(receipt(l));
scheduledDateTime 'Planned date' (ReceiptLine l) = scheduledDateTime(receipt(l)) IN id;
vendor (ReceiptLine l) = vendor(receipt(l));
nameVendor 'Vendor' (ReceiptLine l) = nameVendor(receipt(l));
nameLocation 'Storage' (ReceiptLine l) = nameLocation(receipt(l));

FORM receiptLines 'Receipt lines'
    OBJECTS l = ReceiptLine
    PROPERTIES(l) READONLY scheduledDateTime, number, index, nameProduct

    LIST ReceiptLine OBJECT l
;

edit (ReceiptLine l) + { edit(receipt(l)); } 
           
FORM receipt 'Admission'
    OBJECTS r = Receipt PANEL
    PROPERTIES(r) nameType, scheduledDateTime, number, 
                  nameVendor, nameLocation,
                  note

    OBJECTS l = ReceiptLine
    PROPERTIES(l) index READONLY, nameProduct, nameUom, idBarCodeProduct, idProduct,
                  initialDemand SHOWIF NOT immediate(r) BACKGROUND IF status(receipt(l)) = ReceiptStatus.draft THEN RGB(198,230,247)
    PROPERTIES(l) NEW, DELETE        
    FILTERS receipt(l) = r
   
    EDIT Receipt OBJECT r
;

DESIGN receipt {
    caption = (CONCAT ' ', 'Admission', 'No' + number(r), 'from ' + scheduledDateTime(r));
    OBJECTS {       
        NEW header {
            NEW headerCenter {
                alignment = STRETCH;
                type = CONTAINERH;
                NEW headerLeft {
                    MOVE PROPERTY(nameType(r)) { notNull = TRUE; }
                    MOVE PROPERTY(scheduledDateTime(r));
                    MOVE PROPERTY(number(r));
                }
                NEW headerRight {
                    MOVE PROPERTY(nameVendor(r));
                    MOVE PROPERTY(nameLocation(r)) { notNull = TRUE; }           
                }
            }
            NEW headerBottom {
                alignment = STRETCH;
                MOVE PROPERTY(note(r));           
            }
        }
        NEW details {
            fill = 5;
            type = TABBED;
            NEW lines {
                caption = 'Strings';
                type = SPLITH;
                MOVE BOX(l) {
                    PROPERTY(nameProduct(l)) { notNull = TRUE; }
                    PROPERTY(initialDemand(l)) { notNull = TRUE; }
                }
                NEW linesTab {
                    fill = 0.3;
                    type = TABBED;
                }
            }                  
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

@defineDocHistory(receipt, r, product, initialDemand);

// receipts
FORM receipts 'Receipts'
    OBJECTS r = Receipt
    PROPERTIES(r) READONLY nameType, scheduledDateTime, number, 
                           nameVendor, nameLocation,
                           note, countLines
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE
;

@defineDocStatusForm(receipt, r);
@defineDocLocationAccess(receipt, r);
@defineDateFilterForm(receipts, r, scheduled);

NAVIGATOR {
    operations {
        NEW receipts;
    }
}

// immediate
immediateReceipt 'Unscheduled admission' () {
    NEWSESSION {
        NEW r = Receipt {
            immediate(r) <- TRUE;
            SHOW receipt OBJECTS r = r DOCKED;
        }
    }
} IMAGE 'add.png';

EXTEND FORM receipts 
    PROPERTIES immediateReceipt() DRAW r TOOLBAR
;

// copy
copy 'Copy' (Receipt r) { 
    NEWSESSION {
        NEW nr = Receipt {
            type(nr) <- type(r);
            location(nr) <- location(r);
            vendor(nr) <- vendor(r);
            note(nr) <- note(r);
            immediate(nr) <- immediate(r);
            FOR receipt(ReceiptLine l) = r INLINE NEW nl = ReceiptLine DO {
                receipt(nl) <- nr;
                product(nl) <- product(l);
                initialDemand(nl) <- initialDemand(l);
            }
            SHOW receipt OBJECTS r = nr DOCKED;
        }
    }
}

EXTEND FORM receipt
    PROPERTIES(r) copy 
;

DESIGN receipt {
    secondaryActions {
        MOVE PROPERTY(copy(r));
    }
}
