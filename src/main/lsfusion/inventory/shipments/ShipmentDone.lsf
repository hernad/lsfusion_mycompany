MODULE ShipmentDone;

REQUIRE ShipmentReady;

NAMESPACE Inventory;

EXTEND CLASS ShipmentStatus {
    done 'Shipped'
}
done 'Shipped' = DATA BOOLEAN (Shipment);

status(Shipment s) += WHEN done(s) THEN ShipmentStatus.done;

executionDateTime 'Shipping date' = DATA DATETIME (Shipment);
WHEN SET(done(Shipment s)) AND NOT CHANGED(executionDateTime(s)) DO executionDateTime(s) <- currentDateTime(); 
@defineDateTimeAggregation(Shipment, execution, ' delivery');

executionDateTime 'Shipping date' (ShipmentLine l) = executionDateTime(shipment(l));
executionDate 'Shipping date' (ShipmentLine l) = executionDate(shipment(l));

done 'Shipped' = DATA NUMERIC[16,3] (ShipmentLine);

weightDone 'Weight' (ShipmentLine l) = NUMERIC[16,3](done(l) * weight(product(l)));

CONSTRAINT done(ShipmentLine l) < 0 OR done(l) > initialDemand(l) 
    MESSAGE 'The shipped quantity must be between 0 and original';

setDone 'Fill Shipped' (Shipment s) {
    done(ShipmentLine l) <- reserved(l) WHERE shipment(l) = s;    
}

extraValidate ABSTRACT LIST (Shipment);
validate 'Shipped' (Shipment s) {   
    APPLY; // сохраняем изменения
    IF canceled() THEN RETURN; // если сработало ограничение, то выходим
    
    NEWSESSION {
        IF NOT immediate(s) THEN {
            extraValidate(s);
            
            IF NOT (GROUP SUM 1 IF done(ShipmentLine l) AND shipment(l) = s) THEN
                ASK 'Shipment quantities not filled for shipment ' + number(s) + '. Leave reserved?' DO {
                    setDone(s);
                } ELSE RETURN;
        }
        
        done(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM shipmentLines
    PROPERTIES(l) READONLY executionDateTime AFTER scheduledDateTime(l), done
;

toValidate (Shipment s) = status(s) = ShipmentStatus.ready OR (immediate(s) AND status(s) = ShipmentStatus.draft);

EXTEND FORM shipment
    PROPERTIES(s) validate SHOWIF toValidate(s), 
                  done,
                  setDone DRAW l TOOLBAR SHOWIF status(s) = ShipmentStatus.ready,
                  executionDateTime SHOWIF done(s)
    
    PROPERTIES(l) done BACKGROUND RGB(198,230,247) IF toValidate(s)
;

DESIGN shipment {                               
    primaryActions {                  
        MOVE PROPERTY(validate(s)) FIRST { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(done(s));                              
    } 
    headerLeft {
        MOVE PROPERTY(executionDateTime(s));
    }                  
}

EXTEND FORM shipments
    PROPERTIES(s) READONLYIF isReadonly() BACKGROUND background(s) 
                             executionDateTime AFTER scheduledDateTime(s)
 
    EXTEND FILTERGROUP status
        FILTER 'Shipped' status(s) = ShipmentStatus.done
;

@defineSelectionAction(shipments, Shipment, validate, 'Shipped', statuses);