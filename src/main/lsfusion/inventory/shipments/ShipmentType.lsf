MODULE ShipmentType;

REQUIRE InventorySettings, Numerator, Location;

NAMESPACE Inventory;

CLASS ShipmentType 'Shipment type';
name 'Name' = DATA ISTRING[50] (ShipmentType) NONULL;

id 'Code' = DATA STRING[20] (ShipmentType);
shipmentType = GROUP AGGR ShipmentType t BY id(t);

isTransfer 'Moving' = DATA BOOLEAN (ShipmentType) CHARWIDTH 10;
countTransferTypes = GROUP SUM 1 IF isTransfer(ShipmentType t);

numerator 'Numerator' = DATA Numerator (ShipmentType);
nameNumerator 'Numerator' (ShipmentType o) = name(numerator(o));

// locations
defaultLocation = DATA Location (ShipmentType);
nameDefaultLocation 'Место хранения по умолчанию (откуда)' (ShipmentType o) = name(defaultLocation(o));

defaultToLocation = DATA Location (ShipmentType);
nameDefaultToLocation 'Место хранения по умолчанию (куда)' (ShipmentType o) = name(defaultToLocation(o));

CONSTRAINT defaultToLocation(ShipmentType s) = defaultLocation(s)
           MESSAGE 'Where and where storage locations cannot coincide';

maxQuantity 'Максимальное кол-во товара' = DATA NUMERIC[16,3] (ShipmentType);

FORM shipmentType 'Shipment type'
    OBJECTS o = ShipmentType PANEL 
    PROPERTIES(o) name, id, isTransfer, nameNumerator, 
                  nameDefaultLocation, nameDefaultToLocation SHOWIF isTransfer(o), 
                  maxQuantity
     
    EDIT ShipmentType OBJECT o;
;

DESIGN shipmentType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
        }
    }
}

FORM shipmentTypes 'Delivery types'
    OBJECTS o = ShipmentType
    PROPERTIES(o) READONLY name, id
    
    LIST ShipmentType OBJECT o;
;

EXTEND FORM options
    OBJECTS st = ShipmentType
    PROPERTIES(st) READONLY name, id
    PROPERTIES(st) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(st);
    }
}

// default type
countShipmentTypes 'Number of types' () = GROUP SUM 1 IF r IS ShipmentType; 
firstShipmentType = GROUP LAST ShipmentType r ORDER DESC r WHERE r IS ShipmentType;

defaultShipmentType = firstShipmentType() IF countShipmentTypes() = 1;