MODULE ShipmentPicking;

REQUIRE ShipmentPrint, ShipmentRes, ShipmentInv;

NAMESPACE Inventory;

// type
picking 'Отбор' = DATA BOOLEAN (ShipmentType);
EXTEND FORM shipmentType PROPERTIES(o) picking;

// reserved
reserved 'Зарезервировано' = DATA NUMERIC[16,3] (ShipmentLine, Location);
reservedRec 'Зарезервировано (с учетом вложенных)' (ShipmentLine l) = GROUP SUM reserved(l, Location loc) IF level(loc, location(shipment(l)));
ledgerReserved (ShipmentLine l) += reservedRec(l);

CONSTRAINT reserved(ShipmentLine l, Location loc) < 0 MESSAGE 'Зарезервированое кол-во не может быть меньше 0';
CONSTRAINT reserved(ShipmentLine l) (-) reservedRec(l) < 0 MESSAGE 'Нельзя зарезервировать товара по местам хранения больше чем в строке';

// done
done 'Отгружено' = DATA NUMERIC[16,3] (ShipmentLine, Location);
doneRec 'Отгружено (с учетом вложенных)' (ShipmentLine l) = GROUP SUM done(l, Location loc) IF level(loc, location(shipment(l)));
ledgerDone (ShipmentLine l) += doneRec(l);

CONSTRAINT done(ShipmentLine l, Location loc) < 0 MESSAGE 'Отгруженное кол-во не может быть меньше 0';
CONSTRAINT done(ShipmentLine l) (-) doneRec(l) < 0 MESSAGE 'Нельзя отгрузить товара по местам хранения больше чем в строке';

locations (ShipmentLine l) = GROUP CONCAT name(Location loc) + ' : ' + DOUBLE(done(l, loc)), ', ';
note(ShipmentLine l) += locations(l);

// events
dateReceived 'Дата последнего прихода' (ShipmentLine l, Location loc) = prevDateReceived(loc, product(l));

WHEN LOCAL CHANGED(done(ShipmentLine l)) AND picking(type(shipment(l))) DO 
    done(l, Location loc) <- PARTITION UNGROUP done
                                       LIMIT   reserved(l, loc)
                                       ORDER dateReceived(l, loc), loc
                                       BY l;

available 'Доступно' (ShipmentLine l, Location loc) = prevAvailable(loc, product(l)) (+) PREV(reserved(l, loc));
extraCheckAvailability (Shipment s) + {
    IF picking(type(s)) THEN {
        reserved(ShipmentLine l, Location loc) <- 
                (PARTITION UNGROUP reserved
                           LIMIT   available(l, loc) IF level(loc, location(shipment(l)))
                           ORDER dateReceived(l, loc), loc
                           BY l)                
                                WHERE shipment(l) = s;
    }
}

extraUnreserve (Shipment s) + {
    IF picking(type(s)) THEN {
        reserved(ShipmentLine l, Location ll) <- NULL WHERE shipment(l) = s;
    }
}

extraValidate (Shipment s) + {
    IF picking(type(s)) THEN {
        done(ShipmentLine l, Location loc) <- reserved(l, loc) WHERE shipment(l) = s AND NOT (GROUP SUM done(l, Location ll));
    }
}

EXTEND FORM shipment
    OBJECTS loc = Location
    PROPERTIES READONLY name(loc)
    PROPERTIES READONLY dateReceived(l, loc), available(l, loc), reserved(l, loc)
    PROPERTIES          done(l, loc)

    FILTERS level(loc, location(shipment(l))), 
            available(l, loc) OR reserved(l, loc) OR done(l, loc)
;

DESIGN shipment {
    lines {
        type = SPLITH;
        linesTab {
            MOVE BOX(loc) {
                caption = 'Отбор';
                showIf = (picking(type(s)) AND waiting(s) AND NOT immediate(s));
            }
        }
    }
}

// resledger
CLASS ShipmentPickingRes 'Отгрузка' : ResLedger;

pickingLedger = AGGR ShipmentPickingRes WHERE reserved(ShipmentLine l, Location loc) AND toReserve(shipment(l));

type(ShipmentPickingRes r) += nameType(shipment(l(r)));

dateTime(ShipmentPickingRes r) += executionDateTime(shipment(l(r)));
number(ShipmentPickingRes r) += number(shipment(l(r)));

partner(ShipmentPickingRes r) += customer(shipment(l(r)));
location(ShipmentPickingRes r) += loc(r);

product(ShipmentPickingRes r) += product(l(r));
reserved(ShipmentPickingRes r) += reserved(l(r),loc(r));

// invledger
CLASS ShipmentPickingInv 'Отгрузка' : InvLedger;

invLedger = AGGR ShipmentPickingInv WHERE done(ShipmentLine l, Location loc);

type(ShipmentPickingInv p) += nameType(shipment(l(p)));

active(ShipmentPickingInv p) += active(shipment(l(p)));

dateTime(ShipmentPickingInv p) += executionDateTime(shipment(l(p)));
number(ShipmentPickingInv p) += number(shipment(l(p)));

fromLocation(ShipmentPickingInv p) += loc(p);
toLocation(ShipmentPickingInv p) += toLocation(shipment(l(p)));

partner(ShipmentPickingInv p) += customer(shipment(l(p)));

product(ShipmentPickingInv p) += product(l(p));
quantity(ShipmentPickingInv p) += done(l(p), loc(p));

edit (ShipmentPickingInv p) + { edit(shipment(l(p))); } 