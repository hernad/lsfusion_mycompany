MODULE TransferBulkCreate;

REQUIRE Shipment, ShipmentFilter, LocationSelection;

NAMESPACE Inventory;

bulkCreate 'Create displacements' () {
    IF NOT type() THEN {
        MESSAGE 'Shipment type not selected';
        RETURN;
    }
    IF NOT isTransfer(type()) THEN {
        MESSAGE 'The selected shipment type is not a transfer';
        RETURN;
    }
    IF NOT location() THEN {
        MESSAGE 'No storage location selected (from where)';
        RETURN;
    }
    
    DIALOG dialogLocationsMulti DOCKED DO {
        NEWSESSION NESTED (selected[Location]){
            FOR selected(Location to) DO NEW s = Shipment {
                type(s) <- type();
                location(s) <- location();
                toLocation(s) <- to;
            }
            APPLY;
        }
    }
}

EXTEND FORM shipments
    PROPERTIES() bulkCreate
;

DESIGN shipments {
    actions {
        MOVE PROPERTY(bulkCreate()) { alignment = STRETCH; }
    }
}