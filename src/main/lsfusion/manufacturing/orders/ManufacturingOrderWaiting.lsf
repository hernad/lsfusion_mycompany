MODULE ManufacturingOrderWaiting;

REQUIRE ManufacturingOrder, ManufacturingOrderSelection;

NAMESPACE Manufacturing;

EXTEND CLASS ManufacturingOrderStatus {
    waiting 'Pending'
}
waiting 'Pending' = DATA BOOLEAN (ManufacturingOrder);

status (ManufacturingOrder s) += WHEN waiting(s) THEN ManufacturingOrderStatus.waiting;
colorStatus (ManufacturingOrder s) += WHEN status(s) = ManufacturingOrderStatus.waiting THEN RGB(250,150,157);

markAsToDo 'To work' (ManufacturingOrder s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        waiting(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM manufacturingOrder
    PROPERTIES(o) markAsToDo SHOWIF status(o) = ManufacturingOrderStatus.draft, waiting 
;

DESIGN manufacturingOrder {
    primaryActions {
        MOVE PROPERTY(markAsToDo(o)) { fontStyle = 'bold'; }
    }                
    status {
        MOVE PROPERTY(waiting(o));                               
    }
}

EXTEND FORM manufacturingOrders    
    EXTEND FILTERGROUP status
        FILTER 'Pending' status(o) = ManufacturingOrderStatus.waiting    
;

@defineSelectionAction(manufacturingOrders, ManufacturingOrder, markAsToDo, 'To work', statuses);