MODULE ManufacturingOrderDone;

REQUIRE ManufacturingOrderInProgress;

NAMESPACE Manufacturing;

EXTEND CLASS ManufacturingOrderStatus {
    done 'Completed'
}

done 'Completed' = DATA BOOLEAN (ManufacturingOrder);

executionDateTime 'date of manufacture' = DATA DATETIME (ManufacturingOrder);
WHEN SET(done(ManufacturingOrder o)) AND NOT CHANGED(executionDateTime(o)) DO executionDateTime(o) <- currentDateTime(); 

productsLocation 'Recipient' = DATA Location (ManufacturingOrder);
nameProductsLocation 'Recipient' (ManufacturingOrder m) = name(productsLocation(m));

@defineDocLocationAccess(manufacturingOrder, o, products);

CONSTRAINT done(ManufacturingOrder o) AND NOT productsLocation(o)
    MESSAGE 'No recipient storage location selected in completed manufacturing order'; 

prevOnHandAProductsLocation 'Balance after surgery (recipient)' (ManufacturingOrder o) = 
    prevOnHandA(productsLocation(o), item(o), executionDateTime(o));

markAsDone 'Complete' (ManufacturingOrder m) {
    APPLY;
    IF canceled() THEN RETURN;
        
    NEWSESSION {
        IF NOT produced(m) THEN
            setManufactured(m, toManufacture(m));
        
        ready(m) <- TRUE;
        inProgress(m) <- TRUE;
        done(m) <- TRUE;
        APPLY;
    }
}

status(ManufacturingOrder m) += WHEN done(m) THEN ManufacturingOrderStatus.done;

EXTEND FORM manufacturingOrder
    PROPERTIES(o) markAsDonePrimary = markAsDone SHOWIF status(o) = ManufacturingOrderStatus.inProgress,
                  markAsDoneSecondary = markAsDone SHOWIF (status(o) = ManufacturingOrderStatus.draft OR 
                                                           status(o) = ManufacturingOrderStatus.waiting OR 
                                                           status(o) = ManufacturingOrderStatus.ready), 
                  done, 
                  executionDateTime READONLY SHOWIF done(o),
                  
                  nameProductsLocation
;

DESIGN manufacturingOrder {
    primaryActions {
        MOVE PROPERTY(markAsDonePrimary) { fontStyle = 'bold'; }
    }
    secondaryActions {
        MOVE PROPERTY(markAsDoneSecondary);
    }
    status {
        MOVE PROPERTY(done(o));
    }
    headerLeft {
        MOVE PROPERTY(executionDateTime(o));
    }
    finishedHeader {
        MOVE PROPERTY(nameProductsLocation(o)) { notNull = TRUE; }
    }
}

EXTEND FORM manufacturingOrders
    PROPERTIES(o) READONLYIF isReadonly() BACKGROUND background(o)
                           executionDateTime AFTER scheduledDateTime(o),
                           nameProductsLocation AFTER manufactured(o)
    PROPERTIES(o) READONLY BACKGROUND background(o)                            
                           prevOnHandAProductsLocation READONLY AFTER nameProductsLocation(o)  

    EXTEND FILTERGROUP status
        FILTER 'Completed' status(o) = ManufacturingOrderStatus.done    
;

extraCopy (ManufacturingOrder o, ManufacturingOrder m) + {
    productsLocation(o) <- productsLocation(m);
}

@defineSelectionAction(manufacturingOrders, ManufacturingOrder, markAsDone, 'Complete', statuses);
