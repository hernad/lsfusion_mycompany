MODULE ManufacturingOrderTotal;

REQUIRE ManufacturingOrder, ManufacturingOrderSelection,
        ManufacturingOrderInProgress, ManufacturingOrderCost;

NAMESPACE Manufacturing;

toConsume 'Use up' (Item i) = GROUP SUM toConsume(ConsumedLine l) IF item(l) = i AND selectedManufacturingOrders(manufacturingOrder(l));
consumed 'Spent' (Item i) = GROUP SUM consumed(ConsumedLine l) IF item(l) = i AND selectedManufacturingOrders(manufacturingOrder(l));
cost 'Cost price' (Item i) = GROUP SUM cost(ConsumedLine l) IF item(l) = i AND selectedManufacturingOrders(manufacturingOrder(l));

EXTEND FORM manufacturingOrders
    OBJECTS i = Item
    PROPERTIES(i) READONLY name, nameUom, idBarCode, id,
                           toConsume, consumed, cost
    FILTERS toConsume(i) OR consumed(i)
    ORDERS name(i)
    
    OBJECTS cl = ConsumedLine
    PROPERTIES(cl) READONLY nameType, scheduledDateTime, number, descriptionBom,
                            toConsume, consumed, cost
    PROPERTIES(cl) NEWSESSION EDIT
    FILTERS item(cl) = i,
            selectedManufacturingOrders(manufacturingOrder(cl))
;

DESIGN manufacturingOrders {
    tabbedPane {
        NEW total {
            showIf = countSelectedManufacturingOrders();
            caption = 'Outcome';
            type = SPLITV;
            MOVE BOX(i);
            MOVE BOX(cl);
        } 
    }
}