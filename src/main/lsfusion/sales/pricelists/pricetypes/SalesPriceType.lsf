MODULE SalesPriceType;

REQUIRE SalesSettings, PartnerSales, Partner; 

NAMESPACE Sales;

CLASS PriceType 'Price types';
name 'Name' = DATA ISTRING[100] (PriceType) NONULL CHARWIDTH 10;

id 'Code' = DATA STRING[20] (PriceType);
priceType = GROUP AGGR PriceType t BY id(t);

taxIncluded 'Tax included' = DATA BOOLEAN (PriceType);  

FORM priceType 'Price type'
    OBJECTS pt = PriceType PANEL 
    PROPERTIES(pt) name, id, taxIncluded

    EDIT PriceType OBJECT pt
;

FORM priceTypes 'Types of prices'
    OBJECTS pt = PriceType
    PROPERTIES(pt) READONLY name, id, taxIncluded
    PROPERTIES(pt) NEWSESSION NEW, EDIT, DELETE 

    LIST PriceType OBJECT pt
;   

NAVIGATOR {
    settings {
        NEW priceTypes;
    }
}

priceType = DATA PriceType (Partner);
namePriceType 'Price type' (Partner p) =  name(priceType(p));

EXTEND FORM partner 
    PROPERTIES(p) namePriceType 
;

DESIGN partner {
    customerOptions {
        MOVE PROPERTY (namePriceType(p));
    }
}