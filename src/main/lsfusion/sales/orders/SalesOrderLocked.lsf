MODULE SalesOrderLocked;

REQUIRE SalesOrderSent, SalesOrderConfirmed;

NAMESPACE Sales;

EXTEND CLASS OrderStatus {
    locked 'Closed'
}
locked 'Closed' = DATA BOOLEAN (Order);

status(Order o) += WHEN locked(o) THEN OrderStatus.locked;
colorStatus(Order o) += WHEN status(o) = OrderStatus.locked THEN RGB(255,255,255);

lock 'Close' (Order o){
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        locked(o) <- TRUE;
        APPLY;
    }
}

EXTEND FORM order
     PROPERTIES(o) lock SHOWIF (status(o) = OrderStatus.confirmed), locked;
;

DESIGN order {
    primaryActions {
        MOVE PROPERTY(lock(o)) { fontStyle = 'bold'; };
    }
    status {
        MOVE PROPERTY(locked(o));
    }
}

EXTEND FORM orders    
    FILTERGROUP unlocked
        FILTER 'Open' NOT locked(o) DEFAULT    
;