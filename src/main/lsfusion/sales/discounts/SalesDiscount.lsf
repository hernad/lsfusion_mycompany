MODULE SalesDiscount;

REQUIRE SalesPriceType, BarCode;

NAMESPACE Sales;

CLASS Discount 'A discount';

name 'Name' = DATA STRING[50] (Discount);
manual 'Applied manually' = DATA BOOLEAN (Discount);

startDateTime 'Start of action' = DATA DATETIME (Discount);
startDateTime(Discount d) <- currentDateTime() WHEN SET(d IS Discount);

endDateTime 'End of action' = DATA DATETIME (Discount);

active (Discount d, DATETIME time) = d IS Discount AND time IS DATETIME AND 
                                                    NOT startDateTime(d) > time AND
                                                    NOT endDateTime(d) < time;
active (Discount d) = active(d, currentDateTime()); 

// conditions
fromAmount 'Purchase amount from' = DATA NUMERIC[14,2] (Discount);
fromQuantity 'Number per line from' = DATA NUMERIC[16,3] (Discount);

fromAccumulated 'Sold to a client from' = DATA NUMERIC[14,2] (Discount);
fromAccumulatedPreviousMonth 'Sold to a client in the past month from' = DATA NUMERIC[14,2] (Discount);

// item
in 'C.Tax' = DATA BOOLEAN (Discount, Category);
set (Discount d, Category c) = GROUP LAST in(d, Category cp) ORDER level(c, cp) MATERIALIZED;
categories 'Categories' (Discount d) = GROUP CONCAT name(Category c) IF in(d, c), ',' ORDER name(c), c CHARWIDTH 20;

in 'C.Tax' = DATA BOOLEAN (Discount, Item);
set (Discount d, Item i) = in(d, i) OR set(d, category(i));
items 'Items' (Discount d) = GROUP CONCAT name(Item i) IF in(d, i), ',' ORDER name(i), i CHARWIDTH 30;

// pricetype
in 'C.Tax' = DATA BOOLEAN (Discount, PriceType);
countPriceType (Discount d) = GROUP SUM 1 IF in(d, PriceType t) MATERIALIZED;

// discount
discount 'Discount, %' = DATA NUMERIC[5,2] (Discount);

priceType = DATA PriceType (Discount);
namePriceType 'Price type' (Discount d) = name(priceType(d));

FORM discount 'A discount'
    OBJECTS d = Discount PANEL
    PROPERTIES(d) name, manual,
                  startDateTime, endDateTime,
                  fromAmount, fromQuantity,
                  fromAccumulatedPreviousMonth, fromAccumulated,
                  discount SHOWIF NOT priceType(d), namePriceType

    TREE categories c = Category PARENT parent(c) 
    PROPERTIES name(c) READONLY, in(d, c) BACKGROUND (GROUP SUM 1 IF in(d, Category cc) AND level(cc, c))
    ORDERS name(c)
    
    OBJECTS i = Item
    PROPERTIES(i) READONLY name, nameUom, idBarCode, id
    PROPERTIES in(d, i)
    ORDERS name(i)
    FILTERGROUP inItem
        FILTER 'Included' set(d, i)
    FILTERS level(category(i), c)
    
    OBJECTS t = PriceType
    PROPERTIES(t) READONLY name
    PROPERTIES in(d, t)  
    
    EDIT Discount OBJECT d
;

DESIGN discount {
    OBJECTS {
        NEW general {
            caption = 'Basic';
            type = CONTAINERH;
            alignment = STRETCH;
            MOVE PROPERTY(name(d));
            MOVE PROPERTY(manual(d));
        }
        NEW period {
            caption = 'Validity';
            type = CONTAINERH;
            alignment = STRETCH;
            MOVE PROPERTY(startDateTime(d));
            MOVE PROPERTY(endDateTime(d));
        }
        NEW conditions {
            caption = 'Conditions';
            alignment = STRETCH;
            NEW generalConditions {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(fromAmount(d));
                MOVE PROPERTY(fromQuantity(d));
            }
            NEW accumulated {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(fromAccumulated(d));
                MOVE PROPERTY(fromAccumulatedPreviousMonth(d));
            }
        }
        NEW discounts {
            caption = 'A discount';
            type = CONTAINERH;
            MOVE PROPERTY(discount(d));
            MOVE PROPERTY(namePriceType(d));
        }
        NEW details {
            type = TABBED;
            fill = 1;
            NEW items {
                caption = 'Items';
                type = SPLITH;
                MOVE BOX(TREE categories);
                MOVE BOX(i) { fill = 2; }
            }
            MOVE BOX(t) {
                caption = 'Types of prices';
            }
        }
    }
}

FORM discounts 'Discounts'
    OBJECTS d = Discount
    PROPERTIES(d) READONLY name,
                           startDateTime, endDateTime,
                           fromAmount, fromQuantity,
                           fromAccumulatedPreviousMonth, fromAccumulated,
                           discount, namePriceType,
                           categories, items
    PROPERTIES(d) NEWSESSION NEW, EDIT, DELETE
    
    FILTERGROUP active
        FILTER 'Active' active(d) DEFAULT
    
    LIST Discount OBJECT d
;

NAVIGATOR {
    operations {
        NEW discounts;
    }
}

// meta
META defineDocSalesDiscount(doc)
    salesDiscount = DATA Discount (###doc##Line);
    nameSalesDiscount 'A discount' (###doc##Line l) = name(salesDiscount(l)) CHARWIDTH 10;
    
    changeSalesDiscount(###doc##Line l) {
        DIALOG selectDiscount OBJECTS l = l, d = salesDiscount(l) CHANGE;
    } 
    
    EXTEND FORM doc
        PROPERTIES(l) nameSalesDiscount BEFORE discount(l) ON CHANGE changeSalesDiscount(l)
    ;
    
    WHEN LOCAL CHANGED(salesDiscount(###doc##Line l)) AND NOT priceType(salesDiscount(l)) AND NOT CHANGED(discount(l)) DO
        discount(l) <- discount(salesDiscount(l));
    
    WHEN LOCAL CHANGED(salesDiscount(###doc##Line l)) AND priceType(salesDiscount(l)) AND NOT CHANGED(discountPrice(l)) DO {
        discountPrice(l) <- priceA(priceType(salesDiscount(l)), item(l), dateTime(l));
        recalcDiscount(l);
    }
END